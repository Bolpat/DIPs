
# Formatted String Tuple Literals

| Field           | Value                                                       |
|-----------------|-------------------------------------------------------------|
| DIP:            | 1036                                                        |
| Review Count:   | 1                                                           |
| Author:         | Adam D. Ruppe<br>Steven Schveighoffer schveiguy@gmail.com   |
| Implementation: |                                                             |
| Status:         | Post-Community Round 1                                      |

## Abstract

Instead of requiring a format string followed by an argument list, string interpolation via formatted string tuple literals enables
embedding arguments in the string itself.


## Contents
* [Rationale](#rationale)
* [Prior Work](#prior-work)
* [Description](#description)
* [Breaking Changes and Deprecations](#breaking-changes-and-deprecations)
* [Reference](#reference)
* [Copyright & License](#copyright--license)
* [Reviews](#reviews)

## Rationale

Though the conventional format string/argument list pairing is suitable for
short strings with a small number of arguments, it tends to break down with longer strings
that have many arguments. Omitted arguments, extra arguments, and mismatches
between format specifiers and their corresponding arguments are common errors. Embedding arguments
in format strings can eliminate these errors. Readability is improved and the code is visually
easier to review for correctness.

## Prior Work

* Interpolated strings have been implemented and well-received in many languages.
For many such examples, see [String Interpolation](https://en.wikipedia.org/wiki/String_interpolation).
* [DIP1027](https://github.com/dlang/DIPs/blob/master/DIPs/rejected/DIP1027.md), from which this DIP was derived.
* [C#'s implementation](https://docs.microsoft.com/en-us/dotnet/csharp/language-reference/tokens/interpolated#compilation-of-interpolated-strings) which returns a formattable object that user functions can use
* [Javascript's implementation](https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Template_literals) which passes `string[], args...` to a builder function very similarly to this proposal
* Jason Helson submitted a DIP [String Syntax for Compile-Time Sequences](https://github.com/dlang/DIPs/pull/140).
* [Jonathan Marler's Interpolated Strings](http://github.com/dlang/dmd/pull/7988)

## Description

```D
writefln(i"I ate $apples and ${%d}bananas totalling $(apples + bananas) fruit.");
```
is rewritten as:
```D
writefln(<interpolationSpec>, apples, bananas, apples + bananas);
```

The `interpolationSpec` parameter will have a type defined by DRuntime. The exact type name is unspecified. DRuntime will provide an enum template, `isInterpolationSpec(T)`, which evaluates to `true` iff the type passed in is an interpolation spec. Each `$` token in the string is considered an interpolation parameter.

The `{%d}` syntax is for circumstances when the user needs to provide a format specifier for a given parameter.
What goes between the `{` `}` is unspecified, so this capability can be used by any function
in the present, past, or future, with no need to update the core language or the runtime library. This allows interpolated strings to be agnostic about the format specifications.

The spec can be used as follows.

`spec.toFormatString!(defaultSpec)` produces a compile-time format string with all the interpolated strings replaced as defined after the grammar below, but summarized as:
1. If the prefix `{...}` is put between the `$` and the parameter, the token(s) between the `{` `}` is used.
2. Otherwise, `defaultSpec` is used.

`spec.hasAllSpecs` is a compile-time boolean which indicates whether all interpolation parameters had specs defined with a `{}` prefix.

`spec` will automatically convert to a compile-time NUL-terminated C string if `spec.hasAllSpecs` is true. This allows it to be used for existing C functions such as `printf`.

Example:

```D
void foo(T, Args...)(T spec, Args)
if (isInterpolationSpec!T) // used for overloading with other functions
{
    import std.stdio;
    string fmt = spec.toFormatString!"%s"; // also can be called at compile-time.
    writeln(`D format spec: "`, fmt, `"`);
    static if (spec.hasAllSpecs) // if all specs are specified
    {
        immutable char *zFormat = spec; // automatically converts to a C null-terminated string.
        writeln(`C format spec: "`, zFormat[0 .. strlen(zFormat)], `"`);
    }
}

void main()
{
    int apples = 5;
    int bananas = 6;

    foo(i"I ate $apples and ${%d}bananas totalling $(apples + bananas) fruit.");
    // output:
    // D format spec: "I ate %s and %d totalling %s fruit."

    foo(i"I ate ${%d}apples and ${%d}bananas totalling ${%d}(apples + bananas) fruit.");
    // output:
    // D format spec: "I ate %d and %d totalling %d fruit."
    // C format spec: "I ate %d and %d totalling %d fruit."
}
```
Example of `printf` usage:

```D
printf(i"I ate ${%d}apples and ${%d}bananas totalling ${%d}(apples + bananas) fruit.\n");
```
This becomes (after converting the spec to a NUL-terinated C string):
```D
printf("I ate %d and %d totalling %d fruit.", bananas, apples + bananas);
```
Any other usage of the interpolation spec parameter is not defined by the D specification, and is subject to change at any time.

In particular one should not depend upon the name of the spec type or its implementation details.

The interpolated string starts as a special string token, `InterpolatedString`, which is the same as a
`DoubleQuotedString` but with an `i` prefix and no `StringPostFix`.

```diff
+InterpolatedString:
+   i" DoubleQuotedCharacters "
```

The `InterpolatedString` appears in the parser grammar
as an `InterpolatedExpression`, which is a `PrimaryExpression`.

```diff
+InterpolatedExpression:
+   InterpolatedString
+   InterpolatedString StringLiterals

+StringLiterals:
+   StringLiteral
+   StringLiteral StringLiterals
```

`InterpolatedExpresssion`s undergo semantic analysis similar to `MixinExpression`.
The contents of the `InterpolatedExpression` must conform to the following grammar:

```diff
+Elements:
+    Element
+    Element Elements

+Element:
+    Character
+    '$$'
+    '$' Argument
+    '$' FormatString Argument

+FormatString:
+    '{' FormatString '}'
+    CharacterNoBraces

+CharacterNoBraces:
+    CharacterNoBrace
+    CharacterNoBrace CharacterNoBraces

+CharacterNoBrace:
+    characters excluding '{' and '}'


+Argument:
+    Identifier
+    Expression

+Expression:
+    '(' Expression ')'
+    CharacterNoParens

+CharacterNoParens:
+    CharacterNoParen
+    CharacterNoParen CharacterNoParens

+CharacterNoParen:
+    characters excluding '(' and ')'
```

The `InterpolatedExpression` is converted to a tuple expression, where the first tuple element
is the DRuntime interpolation spec, and the `Argument`s form the remainder of the tuple elements.

The compiler implements the following rules before passing the appropriate data to the runtime for each element:

If the `Element` is:

* `Character`, it is used as part of the format string.
* `'$$'`, a '$' is used as part of the format string.

If a `'$'` occurs without a following `'$'`, this will denote an interpolation parameter.

If the `Element` sequence is:
* `'$' Argument`, then the runtime is instructed to place the default format specifier into the format string. The default format specifier is defined by user code as a template parameter to `spec.toFormatString`.
* `'$' '{' FormatString '}' Argument`, then the runtime is instructed to put `FormatString` into the format string.

If the `Argument` is an `Identifier`, it is appended to the tuple as an `IdentifierExpression`.
If the `Argument` is an `Expression`, it is lexed and parsed (including the surrounding parentheses, similar to `MixinExpressions`; one may think of this as the compiler putting `mixin(...)` around the extracted string) and appended to the tuple as an `Expression`.

Compile-time errors will be generated if the `Elements` do not fit the grammar.

### Diagnostics

An `i""` string is not likely to be compatible with existing D `string` functions and variables. The compiler SHOULD provide helpful hints in error messages to help new users understand how to properly use the new feature. This message MAY link to a web page to educate the user on resolving the error.

```D
void foo(string s) {}

foo(i"test $(4)"); // a CT error due to type mismatch. The compiler should suggest functions (see below) to remedy the user's problem.
```

If the DRuntime implementation symbols are not present in `object.d`, use of interpolated strings MUST result in a compile-time error. The compiler SHOULD issue a user-friendly diagnostic, for example "string interpolation support not found in DRuntime", instead of leaking implementation details in error messages.

### Concatenations

In order to facilitate convenient formatting of long strings, if a `StringLiteral` follows an `InterpolatedString`,
it is appended to the `InterpolatedString` in the parsing pass (not the lexer pass).
A `StringPostfix` may not appear on any of the interpolated string or the following string literal.
Such concatenations enable the various string literal types to be used as `InterpolatedStrings`,
such as:

```D
i""q{apples and $("bananas")}
```
would be identical to:
```D
i"apples and $(\"bananas\")"
```
### Example Implementation

This implementation is provided for reference, but is not necessarily how the compiler and DRuntime will interact when processing interpolated strings. Therefore, though it is useful for discussion, this implementation will NOT be part of the D specification, and the actual implementation may vary.

In this implementation, the compiler uses lowering to provide all the information to the runtime. For example:
```
i"I ate $apples and ${%d}bananas totalling $(apples + bananas) pieces of fruit."
```

Would be lowered by the compiler to the list:
```D
.object._d_interpolated_string!("I ate ", .object._d_interpolated_format_spec(null),
                        " and ", .object._d_interpolated_format_spec("%d"),
                        " totalling ", .object._d_interpolated_format_spec(null),
                        " pieces of fruit."),
    apples, bananas, (apples + bananas)
```
The following new code would be added to object.d:

```D
struct _d_interpolated_string(Parts...) {
        static:

        private bool _hasAllSpecs() {
            assert(__ctfe);
            foreach(part; Parts)
               static if(is(typeof(part) == _d_interpolated_format_spec))
                  if(part.spec is null)
                       return false;
            return true;
        }

        public enum hasAllSpecs = _hasAllSpecs();

        private string toFormatStringImpl(string defaultSpec) {
            assert(__ctfe);
            string ret;
            foreach(part; Parts)
                    static if(is(typeof(part) == _d_interpolated_format_spec)) {
                            if(part.spec is null)
                                    ret ~= defaultSpec;
                            else
                                    ret ~= part.spec;
                    } else static if(is(typeof(part) : const(char)[]))
                            ret ~= part;
                    else static assert(0);
            return ret;
        }

        private immutable(char*) toFormatStringzImpl() {
                return toFormatString!(null).ptr;
        }

        public template toFormatString(string defaultSpec) {
                enum toFormatString = toFormatStringImpl(defaultSpec);
        }

        static if(hasAllSpecs) {
                alias toFormatStringzImpl this;
        }
}

immutable struct _d_interpolated_format_spec {
        string spec;
}

enum isInterpolationSpec(T) = is(T == _d_interpolated_string!P, P...);
```

### Optional `idup` mechanism

As an addition for user-friendliness, we also suggest the addition of an `idup` overload to `object.d` specialized for a tuple that is recognized as starting with an interpolation spec, e.g., 

```D
string idup(I, T...)(I fmt, T args) if (isInterpolationSpec!I) {
        import std.format;
        return format!(I.toFormatString!"%s")(args);
}
```

Since `idup` is an existing symbol, and since this new overload is constrained to just the new type, this has no effect on existing code and does not contribute to namespace pollution. Moreover, since `"string".idup` is an accepted convention for converting `const` strings to the immutable `string` type, this approach is also a natural extension of existing user skills for string assignment. Lastly, it is known that `.idup` invokes the GC and its associated allocation, so it should come to no surprise that `i"".idup` does as well. However, the `idup` implementation for normal strings does not depend on Phobos while this new implementation does; this is a hidden implementation detail that can be improved upon in the future and strikes the best current balance between usability and elegance of implementation.

### Usage in existing string-accepting functions

No doubt there are many functions that accept a format string followed by its parameters. Such overloads can use the `isInterpolationSpec` test to ensure they do not clash with the normal string overload.

For example, `std.stdio.writefln` can be amended with the following overload:

```D
auto writefln(Fmt, Args...)(Fmt fmt, Args args) if (isInterpolationSpec!Fmt)
        return std.stdio.writefln!(fmt.toFormatString!"%s", Args)(args);
}
// ditto for formattedWrite, format, and any other uses.
```

These ensure that interpolated strings _just work_ for their most likely target functions while also providing a new benefit: the format string, including user additions via `${}`, will be checked at compile time, even when passed as a run-time argument.

```D
string name;
writefln(i"Hello, ${%d}name"); // causes a compile error for invalid format specification
```

Note that this is possible despite the compiler itself having no knowledge of format specifiers; it is implemented entirely in library code, and third parties may provide their own formats and checks.

### Justifications

The complexity of the format spec may seem superfluous, however it serves four key roles:

1. It divorces the compiler entirely from details of generated format strings. For example, different functions that accept format strings might use different default format specifiers.
2. It allows overloading existing string-accepting functions to prevent accidental usage that happens to fit the parameter list (see example below).
3. It provides necessary error checking capabilities.
4. It provides an additional API for user functions to introspect the string, building on D's existing compile-time capabilities.

DIP1027 proposed a simple string literal as the first argument passed. This is problematic because it introduces potentially subtle errors in likely usage.

#### Wrong-use in unrelated function

Consider a function in D today:

```D
/++
	Creates a window with the given title, width, and height. If
	width or height are set to zero (the default), your window will
	be automatically sized.
+/
Window createWindow(string title, int width = 0, int height = 0);
```

A user has some code that calls it like so:

```D
import std.conv;
auto window = createWindow("Process debugger " ~ to!string(pid));
```

A new version of the compiler is released and the user, eager to try D's new string interpolation feature, rewrites that code as would be common in many other languages:

```D
// look at this clean new code :D
auto window = createWindow(i"Process debugger $pid");
```

This would compile without error, but would not do what the user intended. D would pass the variable `pid` as the second argument to the function, which would interpret it as window width. Without a way to detect this misuse, which is likely to be a common mistake made by programmers more familiar with string interpolation in other languages, users will be unpleasantly surprised with buggy code.

The `isInterpolationSpec` check provides such a way to detect misuse. In fact, the library author doesn't have to do anything---the user will see a helpful error message from the compiler and perhaps try `i"Process debugger $pid".idup` instead, or another suitable alternative.

As an added benefit, a library author can choose to adapt to the interpolated string while keeping it separate from its existing default arguments by way of overloading the first argument on the new type.

Other alternatives discussed in the D community included a compiler-recognized attribute on the parameter to indicate it takes a format string, but once rules are defined to account for all the edge cases in ABI, mangling, overloading, etc., such a thing would have simply reinvented a struct type in a more awkward fashion. It is better to lean on rules the language and its users already understand than invent special rules for this one case.

#### On implicit conversions

To avoid unintentional bugs in libraries that don't anticipate interpolated strings, this DIP does **not** recommend implicit conversion of the interpolation spec structure to `string`, excepting special circumstances. In the example DRuntime implementation above, we include `static if(hasAllSpecs)` as a condition for `alias this`.

The logic behind this decision is that, when making the additional effort to explicitly write format specifiers for all interpolated values, the user will have demonstrated significant understanding of how the feature works both in general and for a specific use case. In that scenario, we can allow implicit conversion so the interpolated string can be used by existing functions.

```
// The user knew enough to write %x for each item, let's trust
// that they understand the caveats of printf and allow this to happen.
printf("${%d}count ${%s}(name.toStringz)(s) available.");
```

This gives maximum compatibility with existing functions balanced against the safety of accidentally calling existing functions inappropriately.

Whereas most existing functions that can use this are C functions, `alias this` suffers a current limitation of allowing `string` or `const(char*)`, but not both. In the example code, we chose to use the C-compatible version instead of the D-compatible one. If `alias this` is enhanced in the future, perhaps we may allow that implicit conversion as well.

Additionally, when using D functions, one may generally call `.idup` on the interpolated string or, if avoiding garbage collected memory, call another function to commit the string into an alternate buffer. Since Phobos functions can (and should) be overloaded on the new type, the user is unlikely to require additional implicit conversions.

#### Interpolation to different formats

Consider an existing D library for interacting with databases. It includes a function:

```D
/++
	Performs a query against the database. The `sql` string
	should consist of the SQL commands with `?n` placeholders
	to represent the given argument.

	Note that SQL placeholders are 1-based, meaning `?1` refers
	to the first argument given, aka `args[0]` in D.

	WARNING:
	Do NOT attempt to use string concatenation to inject data into
	the sql string - doing so puts you at risk of injection attacks.
	Always use the `args` facility with `?n` placeholders.
+/
Result query(T...)(string sql, T args) {}
```

It may even offer a version with `sql` as a compile-time string argument in order to more effectively cache and reuse query handles and, when possible, offer compile-time checks in D analogous to `writefln(x, args)` vs `writefln!x(args)`.

A user with some understanding of D's new string interpolation feature may attempt to use it here:

```D
query(i"Select name from people where age > $min_age");
```

If the spec is a plain string, that will compile successfully but throw a sql syntax error. The user may then realize the addition of a specifier is required:

```D
query(i"Select name from people where age > ${?1}min_age");
```

This works, but it is a little awkward. If the condition is changed:

```D
query(i"Select name from people where name like ${?1}pattern and age > ${?2}min_age");
```

The user had to mannually fix up all subsequent placeholder numbers. With this DIP, the database library can add an overload that provides a searchable default and, at compile-time, replaces the generated string with one that has the correct numbered sequence of placeholders. This is done transparently to the user, without risking any breakage of the existing `query` functions. A possible improvement in the future might support more sophisticated format string generation, allowing for the database library to do this directly using the format spec.

However, a word of warning. Suppose the user tried `query(i"Select name from people where age > $min_age");` and the library did not overload for it. The compiler may suggest "did you try .idup?" in its error message and point the user toward coercing it into a traditional `string` argument. The documentation warns against *concatenation*, not *interpolation*... yet `.idup` would give the same result. The library cannot catch this user error after the fact.

Realistically, this is no worse than the user writing `"age > " ~ max_age` (except in that it is more convenient), but we recommend the compiler diagnostic output a web link with more detail than just writing "try .idup".

#### Conversion to string

The following will give a type mismatch error:

```D
string s = i"";
```

With our recommended `idup` overload in `object.d`, using this is no more difficult than using a `const char[]` assignment to `string` without requiring an additional import from the user.

Other functions should also be available and explained on a web site for users to learn, including, but not limited to, a solution for `@nogc` users. `snprintf` may play that role if the user provides specifiers as detailed above in the section covering implicit conversion.

### Limitations

#### All format specifiers must be known at compile time

It is impossible to provide a format specifier `i"${this_part}foo` at run time. However, one could call a formatting function inside the expression, like `i"$(some_formatter(foo))"`.

#### Mixing conventional format arguments with interpolated strings in legacy functions

Interpolated string formats cannot be mixed with conventional elements unless the receiving function overloads on the interpolation spec type.

```D
const char* tool = "hammer";
printf(i"hammering %s with ${%s}tool", "nails");
```
will produce:
```D
const char* tool = "hammer";
printf("hammering %s with %s", tool, "nails");
```
as the interpolated arguments will always come first, and out of order with the conventional
arguments. This error is not detected by the compiler, but since the user must proactively write
a format specifier, it ought not to happen by accident too frequently.

With D functions like `writefln` providing an overload specifically for interpolated strings,
this can be detected and treated as a compile-time error (which the sample library implementation
would do), corrected to `%%s`, or even interleaved correctly by library code.

Making these work implicitly would mean sacrificing the type safety identified in the `createWindow` case mentioned previously, or baking knowledge of format strings into the language (and a rule added so that user-defined functions can utilize it), but this comes back to awkwardly reinventing a type.

Note that with a small improvement to provide the count of the interpolation parameters, subsequent interpolation strings could be used inside `writefln`.

#### W and D Interpolated Strings

`wchar` and `dchar` interpolated strings are not allowed at this time. If they were to be added, however, `i"..."w` would work the same way, except the interpolation spec would be configured to return `wstring` when calling `spec.toFormatString`.

## Breaking Changes and Deprecations

Since the interpolated string is a new token, no existing code is broken.

## Reference

## Copyright & License
Copyright (c) 2020 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

### Community Review Round 1

[Reviewed Version](https://github.com/dlang/DIPs/blob/15537f9b36afa48f1c1cd57468e8c77f8f2499dd/DIPs/DIP1036.md)

[Discussion](https://forum.dlang.org/post/vwrbvacnuerasbphgtjy@forum.dlang.org)

[Feedback](https://forum.dlang.org/post/vpgemqdqgorsuzqbxazw@forum.dlang.org)

* Since the new syntax does not allow initialization/assignment, it's usage is limited and unintuitive, and the new syntax should be replaced with a template or a trait. One DIP author replied that attempts to use the new syntax that way will result in an informative error instructing the user to call `.idup`.
* The interface the new feature exposes is "hard to use correctly, and hard to use incorrectly" and should be withdrawn. One DIP author disagreed, saying that the interface is easy to use.
* `printf` should not comprise most of the feature's design considerations. One DIP author replied that it does not.
* The conversion of `spec` to a `compile-time NUL-terminated C string` should not be implemented. One DIP author replied whether or not this is useful is a jugement call.
* The syntax `${%d}bananas` is unintuitive. `${bananas:2f}` is better, as is Python's approach to format specifiers. A DIP author replied that the suggested syntax is syntactically ambiguous. The selected syntax came from DIP1027, but if an alternative, unambiguous syntax is preferred by the maintainers, the authors will modify this proposal.
* Format specifiers need to be "100% customizable". A DIP author replied that they are.
* The interpolated string struct should contain its own arguments; the proposed approach of rewriting  to `<interplationSpec>, arg1, arg2` is confusing and prohibits implementing functions which accept two interpolated string arguments. A DIP author replied that a single structure approach has issues with forawrding ref-ness, binding to aliases and lazy parameters, etc.
* The DIP does not provide a good motivation for automatically concatenating string literals to interpolated strings, especially given that automatic string concatenation was deprecated. A DIP author replied that this was copied from DIP1027; the formation of interpolation string tuples across multiple lines and multiple string types should be supported and implicit conversion does the job, but the authors are open to other ideas.
* The DIP does not address combining token strings to generate D code. A DIP author provided an example.
* Given that users want the ability to use interpolated string in assignment and initialization, the DIP should provide a good explanation as to why the compiler should not rewrite assignments to automatically use `.idup`. A DIP author replied that `.idup` is a convenience feature that is not tied to acceptance or rejection of the DIP, but the authors are open to such rewrites if the language maintainers require it.
* The DIP does not account for operator overloading on strings. A DIP author that the specific example given was not possible to support, but it can work with custom types.
* The fear of GC is not sufficient motivation for requiring users to call `.idup`. A DIP author replied that the motivation was not GC avoidance, but due to compatibility with a variety of use cases, such as `ref` parameters, types with no `toString` implementation, compile-time arguments, and more, and suggested the DIP should mention these cases explicitly.
* The DIP leaves unspecified the name of the interplation spec to be defined by DRuntime, but does specify the name `isInterpolationSpec`. This should also be unnamed. A DIP author replied that there must be a way to determine if a function has been given an interpolation spec and it must have a name. With this approach, the implementation has more discretion.
* The DIP explains `toFormatString` and `hasAllSpecs` in great detail; perhaps these should be optional like `.idup`, allowing the DIP to be simplified. A DIP author replied that `toFormatString` is "the only official way to fetch the spec text", and that `hasAllSpecs` is a mechanism to convert to `const(char)*` to which allowing access might not have been necessary.
* The optional `alias this` when all format specifiers are defined is complicated; perhaps an implementation-defined function, like `toFormatz`, can be added. A DIP author replied that this will not work; only the format specifier needs to be translated and such an approach would require passing through all the parameters.
* It's easy to write a wrapper around C functions like `printf`, but the support for implicit conversion to `immutable(char)*` is excessive. A DIP author agreed, saying the initial motivation was to retain DIP1027's ability to call C vararg functions without modifications, but the authors have since decided the requirement "does not carry its own weight".
* The grammar is incorrect, as `Expression` already exists. The DIP author agreed, saying the grammar has multiple issues that need to be corrected.
* Interpolated strings should work "out of the box" with `mixin` and not require `.idup`. A DIP author replied that this will be solved in the next DIP revision.
* The concatenation syntax of `i"" q{$a + $b}` should be simplified to `iq{$a + $b}`. The DIP authors agree.
* In the "Wrong-use in unrelated function", the example provided mentions the situation where a user rewrites code "as would be common in many other languages"; it is impossible "to constrain D based on what people expect from 'many other languages'". A DIP author replied the authors only were referring to how other languages handle string interpolation specifically, and that the next revision of the DIP will solve this in a "cleaner way".
* The DIP needs a much better rationale as to why it's preferable to DIP1027. A DIP author replied that the next revision will be "vastly different" from DIP1027, so it's a moot point.
